import math
import gl
import glscene
import font_styles

class Codeview extends Text{

    load( FontStyles )

	// Base node markers
	_Id:1.
	_Value:2.
	_This:3.
	_Array:4.
	_Object:5.
	_Index:6.
	_Key:7.
	_ThisCall: 8.

	_Block:9.
	_List: 10.
	_Comprehension:11.
	_Template: 12.
	_Break:13.
	_Continue:14.
	_Label:15.

	_If:16.
	_Switch:17.
	_Case:18.

	_Throw:19.
	_Try:20.

	_While:21.
	_DoWhile:22.
	_For:23.
	_ForIn:24.
	_ForOf:25.
	_ForFrom:26.
	_ForTo:27.

	_Var:28.
	_TypeVar:29.
	_Struct:30.
	_Define:31.
	_Enum:32.

	_Def:33.

	_Function:34.
	_Return:35.
	_Yield:36.
	_Await:37.

	_Unary:38.
	_Binary:39.
	_Logic:40.
	_Assign:41.
	_Update:42.
	_Condition:43.

	_New:44.
	_Call:45.
	_Nest:46.

	_Class:47.
	_Signal:48.
	_Quote:49.
	_AssignQuote:50.
	_Rest:51.
	_Then:52.
	_Comment:53.

	// second level markers
		_Id:1.
		_Paren:2.
			_ParenL:1.
			_ParenR:2.
		_Brace:3.
			_BraceL:1.
			_BraceR:2.
		_Bracket:4.
			_BracketL:1.
			_BracketR:2.

		_Comma:5.
		_Colon:6.
		_DColon:7.
		_Dot:8.
		_SemiColon:9.

		_Operator:10.
			_Plus:1.
			_Min:2.
			_Div:3.
			_Mul:4.
			_Pow:5.
			_Shl:6
			_Shr:7
			_EQ:8.
			_NEQ:9.
			_GT:10.
			_LT:11.
			_GTE:12.
			_LTE:13.
			_Plusplus:14.
			_Minmin:15.
			_Assign:16.
			_PlusAssign:17.
			_MinAssign:18.
			_MulAssign:19.
			_DivAssign:20.
			_ShlAssign:21.
			_ShrAssign:22.
			_TerniaryQ:23.
			_TerniaryC:24.
			_Signal:25.
		_String:11.
		_Number:12.
		_Tab:13.
		_Keyword:14.
		_Color:15.
		// Keywordy things

	// Helper functions

	DColon(type, group){
		pack_mark('::', group, type, _DColon)
	}

	Dot(type, group){
		pack_mark('.', group, type, _Dot)
	}

	Colon(type, group){
		pack_mark(':', group, type, _Colon)
	}

	SemiColon(type, group){
		pack_mark(':', group, type, _SemiColon)
	}

	Comma(type, group){
		pack_mark(',', group, type, _Comma)
	}

	ParenL(type, group){
		pack_mark('(', group, type, _Paren, _ParenL)
	}

	ParenR(type, group){
		pack_mark(')', group, type, _Paren, _ParenL)
	}
	BraceL(type, group){
		pack_mark('{', group, type, _Brace, _BraceL)
	}

	BraceR(type, group){
		pack_mark('}', group, type, _Brace, _BraceR)
	}

	BracketL(type, group){
		pack_mark('[', group, type, _Bracket, _BracketL)
	}

	BracketR(type, group){
		pack_mark(']', group, base, _Bracket, _BracketR )
	}

	Operator(op, type, group){
		var col
		if(op == '++') col = _Plusplus
		if(op == '--') col = _Minmin
		if(op == '+') col = _Plus
		if(op == '-') col = _Min
		if(op == '/') col = _Div
		if(op == '*') col = _Mul
		if(op == '**') col = _Pow
		if(op == '==') col = _EQ
		if(op == '!=') col = _NEQ
		if(op == '>') col = _GT
		if(op == '<') col = _LT
		if(op == '=>') col = _GTE
		if(op == '<=') col = _LTE
		if(op == '<<') col = _Shl
		if(op == '>>') col = _Shr
		if(op == '=') col = _Assign
		if(op == ':=') col = _Signal
		if(op == '+=') col = _PlusAssign
		if(op == '-=') col = _MinAssign
		if(op == '/=') col = _DivAssign
		if(op == '*=') col = _MulAssign
		if(op == '<<=') col = _ShlAssign
		if(op == '>>=') col = _ShrAssign
		if(op == '?') col = _TerniaryQ
		if(op == ':') col = _TerniaryC

		pack_mark(op, group, type, _Operator, col)
	}

	Keyword(value, type, group){
		pack_mark(value, group || 0, type, _Keyword)
	}

	// AST Expander

	expand(n){
		if(n)this[n.type](n)
	}

	Program(n){}

	Empty(n){}

	pack_mark(text, group, l1, l2, l3){
		if(l1 && l1._ast_)l1 = l1.value
		if(l2 && l2._ast_)l2 = l2.value
		if(l3 && l3._ast_)l3 = l3.value
		mark(text, group, 65536*(l1||0)+256*(l2||0)+(l3||0))
	}

	Id(n){//{ name:0, flag:0, typing:1 },
		if(n.flag){
			pack_mark('#'+n.name, group++, _Id, _Color)
		}
		else
			pack_mark(n.name, group++, _Id, _Id)
	}

	Value(n){//: { value:0, raw:0, kind:0, multi:0 },
		if(n.kind == 'num')
			pack_mark(n.raw, group++, _Value, _Number)
		else if(n.kind == 'string')
			pack_mark(n.raw, group++, _Value, _String)
		else
			pack_mark(n.raw, group++, _Value)
	}
	
	This(n){//: { },
		pack_mark('this', group++, _This, _Keyword)
	}

	Array(n){//: { elems:2 },
		var mygroup = group++
		BracketL(_Array, mygroup)
		for(var i, elem from n.elems){
			if(i) Comma(_Array, group++)
			expand(elem)
		}
		BracketR(_Array, mygroup)
	}
	Object(n){//: { keys:3 },
		var mygroup = group++
		BraceL(_Object, mygroup)
		for(var prop from n.keys){
			expand(prop.key)
			Colon(_Object)
			expand(prop.value)
		}
		BraceR(_Object, mygroup)
	}
	Index(n){//: { object:1, index:1 },
		expand(n.object)
		var mygroup = group++
		BracketL(_Index, mygroup)
		expand(n.index)
		BracketR(_Index, mygroup)
	}
	Key(n){//: { object:1, key:1, exist:0 },
		expand(n.object)
		Dot(_Key)
		expand(n.key)
	}
	ThisCall(n){//: { object:1, key:1 },
		expand(n.object)
		DColon(_ThisCall)
		expand(n.key)
	}

	Comment(text){
		pack_mark(' // '+text, group++, _Comment)
	}

	class CollectComments extends AST.Walk{
		Pre(n){
			if(n.comments) output.push.apply(output, n.comments)
		}
		Block(n){
			// dont go into a block
		}
	}

	Block(n){//:{ steps:2 },

		var mygroup = group++
		BraceL(_Block, mygroup)
		var old_indent = indent
		indent++
		newline()
		if(n.steps.length) tab(indent)
		else tab(old_indent)
		for(var len, i, step from n.steps){
			expand(step)
			var last = undefined
			// we need to collect the comments
			var comments = CollectComments.output = []
			CollectComments.run(step)
			var skipped = false
			for(var comment from comments){
				if(comment == -1 && last == -1 && !skipped){
					newline()
					tab(indent)
					skipped = true // dont skip more than one line
				}
				if(comment !== -1) Comment(comment)
				last = comment
			}
			newline()
			if(i == len - 1) tab(old_indent)
			else tab(indent)
		}
		indent = old_indent
		BraceR(_Block, mygroup)
	}
	List(n){//: { items:2 },
		for(var i, item from n.items){
			if(i) Comma(_List)
			expand(item)
		}
	}

	Comprehension(n){//:{ for:1, expr:1 },
	}
	Template(n){//: { chain:2 },
	}

	Break(n){//: { label:1 },
		Keyword('break', _Break)
	}
	Continue(n){//: { label:1 },
		Keyword('continue', _Continue)
	}
	Label(n){//: { label:1, body:1 },
		expand(n.label)
		expand(n.body)
	}
	If(n){//: { test:1, then:1, else:1, postfix:0, compr:0 },
		Keyword('if', _If)
		var mygroup = group++
		ParenL(_If, mygroup)
		expand(n.test)
		ParenR(_If, mygroup)
		expand(n.then)
		if(n.else){
			newline()
			tab(indent)
			Keyword('else', _If)
			space()
			expand(n.else)
		}
	}
	Switch(n){//: { on:1, cases:2 },
		Keyword('switch', _Switch)
		var mygroup = group++
		ParenL(_Switch, mygroup)
		expand(n.on)
		ParenR(_Switch, mygroup)
		BraceL(_Switch, mygroup)
		for(var c from n.cases){
			expand(c)
		}
		BraceR(_Switch, mygroup)
	}
	Case(n){//: { test:1, then:2 },
		Keyword('case', _Case)
		expand(n.test)
		Colon(_Case)
		expand(n.then)
	}
	Throw(n){//: { arg:1 },
		Keyword('throw', _Throw)
		space()
		expand(n.arg)
	}
	Try(n){//: { try:1, arg:1, catch:1, finally:1 },
		Keyword('try', _Try)
		expand(n.try)
		Keyword('catch', _Try)
		var mygroup = group++
		ParenL(_Try, mygroup)
		expand(n.arg)
		ParenR(_Try, mygroup)
		expand(n.catch)
		if(n.finally){
			Keyword('finally', _Try)
			expand(n.finally)
		}
	}
	While(n){//: { test:1, loop:1 },
		Keyword('while', _While)
		var mygroup = group++
		ParenL(_While, mygroup)
		expand(n.test)
		ParenR(_While, mygroup)
		expand(n.loop)
	}
	DoWhile(n){//: { loop:1, test:1 },
		Keyword('do', _Do)
		expand(n.loop)
		Keyword('while', _Do)
		var mygroup = group++
		ParenL(_Do, mygroup)
		expand(n.test)
		ParenR(_Do, mygroup)
	}
	For(n){//: { init:1, test:1, update:1, loop:1, compr:0 },
		Keyword('for', _For)
		expand(n.init)
		SemiColon(_For)
		space()
		expand(n.test)
		SemiColon(_For)
		space()
		expand(n.update)
		expand(n.loop)
	}
	ForIn(n){//: { left:1, right:1, loop:1, compr:0 },
		Keyword('for', _For)
		var mygroup = group++
		ParenL(_For, mygroup)
		expand(n.left)
		space()
		Keyword('in', _For)
		space()
		expand(n.right)
		ParenR(_For, mygroup)
		expand(n.loop)
	}
	ForOf(n){//: { left:1, right:1, loop:1, compr:0 },
		Keyword('for', _For)
		var mygroup = group++
		ParenL(_For, mygroup)
		expand(n.left)
		space()
		Keyword('of', _For)
		space()
		expand(n.right)
		ParenR(_For, mygroup)
		expand(n.loop)
	}
	ForFrom(n){//: { right:1, left:1, loop:1, compr:0 }, // right comes first for type inference
		Keyword('for', _For)
		var mygroup = group++
		ParenL(_For, mygroup)
		expand(n.left)
		space()
		Keyword('from', _For)
		space()
		expand(n.right)
		ParenR(_For, mygroup)
		expand(n.loop)
	}
	ForTo(n){//: { left:1, right:1, loop:1, in:1, compr:0 },
		Keyword('for', _For)
		var mygroup = group++
		ParenL(_For, mygroup)
		expand(n.left)
		space()
		Keyword('to', _For)
		space()
		expand(n.right)
		ParenR(_For, mygroup)
		expand(n.loop)
	}
	Var(n){//: { defs:2, const:0 },
		Keyword('var', _Var)
		space()
		for(var i, def from n.defs){
			if(i) Comma(_Var), space()
			expand(def)
		}
	}
	TypeVar(n){//: { typing:1, defs:2, dim:1 },
		Keyword(n.typing.name, _TypeVar)
		space()
		for(var i, def from n.defs){
			if(i) Comma(_Var), space()
			expand(def)
		}
	}
	Struct(n){//: { id:1, struct:1, base:1, defs:2, dim:1 },
		Keyword('struct', _Struct)
		space()
		expand(n.id)
		expand(n.struct)
	}
	Define(n){//: { id:1, value:1 },
		Keyword('define', _Define)
		space()
		expand(n.id)
		space()
		expand(n.value)
	}
	Enum(n){//: { id:1, enums:2 }, 
		Keyword('enum', _Enum)
		space()
		expand(n.id)
		BraceL(_Enum)
		newline()
		for(var e from n.enums){
			expand(e)
		}
		BraceR(_Enum)
	}

	Def(n){//: { id:1, init:1, dim:1 },
		expand(n.id)
		if(n.init){
			space()
			Operator('=', _Def, group++)
			space()
			expand(n.init)
		}
	}

	Function(n){//: { id:1, name:1, params:2, rest:1, body:1, arrow:0, gen:0, def:0 },
		if(n.arrow){

		}
		if(n.name) expand(n.name)
		//else Keyword('function', _Function)
		if(n.id) space(), expand(n.id)
		var mygroup = group++
		ParenL(_Function,mygroup)
		for(var i, param from n.params){
			if(i) Comma(_Function), space()
			expand(param)
		}
		if(n.rest){
			if(i) Comma(_Function), space()
			expand(n.rest)
		}
		ParenR(_Function,mygroup)
		expand(n.body)
	}
	Return(n){//: { arg:1 },
		Keyword('return', _Return)
		if(n.arg) space(), expand(n.arg)
	}
	Yield(n){//: { arg:1 },
		Keyword('yield', _Yield)
		if(n.arg) space(), expand(n.arg)
	}
	Await(n){//: { arg:1 },
		Keyword('await', _Await)
		if(n.arg) space(), expand(n.arg)
	}
	Unary(n){//: { op:0, prefix:0, arg:1 },
		if(n.prefix){
			if(n.op.length!=1)
				Keyword(n.op, _Unary), space()
			else
				Operator(n.op, _Unary)
			expand(n.arg)
		}
		else{
			expand(n.arg)
			Operator(n.op, _Unary)
		}
	}
	Binary(n){//: { op:0, prio:0, left:1, right:1 },
		var paren_l = AST.needsParens(n, n.left)
		var paren_r = AST.needsParens(n, n.right)
		var mygroup = group++
		if(paren_l) ParenL(_Binary,mygroup)
		expand(n.left)
		if(paren_l) ParenR(_Binary,mygroup)
		space()
		Operator(n.op, _Binary, group++)
		space()
		if(paren_r) ParenL(_Binary,mygroup)
		expand(n.right)
		if(paren_r) ParenR(_Binary,mygroup)
	}
	Logic(n){//: { op:0, prio:0, left:1, right:1 },
		var paren_l = AST.needsParens(n, n.left)
		var paren_r = AST.needsParens(n, n.right)
		var mygroup = group++
		if(paren_l) ParenL(_Logic,mygroup)
		expand(n.left)
		if(paren_l) ParenR(_Logic,mygroup)
		space()
		Operator(n.op, _Logic, group++)
		space()
		if(paren_r) ParenL(_Logic,mygroup)
		expand(n.right)
		if(paren_r) ParenR(_Logic,mygroup)
	}
	Assign(n){//: { op:0, prio:0, left:1, right:1 },
		expand(n.left)
		space()
		Operator(n.op, _Assign, group++)
		space()
		expand(n.right)
	}
	Update(n){//: { op:0, prio:0, arg:1, prefix:0 },
		if(prefix)
			Operator(n.op, _Update), expand(n.arg)
		else
			expand(n.arg), Operator(n.op, _Update)
	}
	Condition(n){//: { test:1, then:1, else:1 },
		expand(n.test)
		Operator('?', _Condition)
		expand(n.then)
		Operator(':', _Condition)
		expand(n.else)
	}
	New(n){//: { fn:1, args:2 },
		Keyword('new', _New)
		space()
		expand(n.fn)
		ParenL(_New)
		for(var i, args from n.args){
			if(i) Comma(_New), space()
			expand(args)
		}
		ParenR(_New)
	}

	Call(n){//: { fn:1, args:2 },
		var mygroup = group++
		expand(n.fn)
		ParenL(_Call, mygroup)
		var hasout =0 
		var post, type
		for(var len, i, args from n.args){
			if(n.extarg == 'do' && i == len - 1){
				type = 'do'
				post = args
			}
			else if(n.extarg == 'on' && i == 0){
				type = 'on'
				post = args
			}
			else{
				if(hasout) Comma(_Call), space()
				expand(args)
				hasout = 1
			}
		}
		ParenR(_Call, mygroup)
		if(post){
			space()
			Keyword(type, _Call)
			expand(post)
		}
	}

	Nest(n){//: { fn:1, body:1, arrow:0 },
		expand(n.fn)
		expand(n.body)
	}
	Class(n){//: { id:1, base:1, body:1 },
		Keyword('class', _Class)
		space()
		expand(n.id)
		if(n.base){
			space()
			Keyword('extends', _Class)
			space()
			expand(n.base)
		}
		expand(n.body)
	}
	Signal(n){//: { left:1, right:1 },
		expand(n.left)
		Operator(':=', _Signal)
		expand(n.right)
	}
	Quote(n){//: { quote:1 },
		Operator(':', _Quote, group++)
		expand(n.quote)
	}
	AssignQuote(n){//: { left:1, quote:1 },
		expand(n.left)
		Operator(':', _AssignQuote, group++)
		expand(n.quote)
	}
	Rest(n){//: { id:1, dots:0 },
		Operator('...', _Rest, group++)
		expand(n.id)
	}
	Then(n){//: { name:1, do:1 },
	}
	Debugger(n){//: { },
	}
	With(n){//: { object:1, body:1 }
	}

	get ast(v){
		return __ast
	}
	set ast(v){
		__ast = v
		// ok lets turn this AST into an actual codeview.
		if(is_initialized){
			alloc(1000, true)
			indent = 0
			group = 0
			expand(v)
			update()
		}
	}

	constructor(){
		Text::constructor()
		ast = __ast
	}

	style:(){
		vec4 m = marks[] // magical comments!
		vec2 pos = m.xy 
		vec2 center = pos - .5

		var group = m.z
		var type = floor(m.w / 65536.)
		var sub = mod(floor(m.w/256.), 256.)
		var part  = mod(m.w, 256.)
		
		if(sub == _Paren || sub == _Brace || sub == _Bracket){
			if(sub == _Paren){
				u_color = mix(#aero, #magenta, abs on sin on group)
				u_boldness = 0.4 + 0.3*sin on group
			}
			else if(sub == _Bracket){
				u_color = #ccc//mix(#red, #yellow, abs on sin on group)
				u_boldness =  .3*sin on group
			}
			else{
				u_color = #yellow//mix(#red, #yellow, abs on sin on group)
				u_boldness =  .3*sin on group
			}
			u_outline = true
			u_outline_thickness = (1.+.6*sin(group - 6.*pos.y + 4.*time.sec)) / u_scaling
		}
		else if(sub == _Operator){
			u_color = mix(#red, #yellow, abs on sin(0.05*group + length(center) + time.sec))
			u_boldness = .5+0.7sin(8.*length(center)-4.*time.sec+group)
		}
		else if(type == _Id){
			u_color = #white
			if(sub == _Color){
				u_color = #pink
			}
		}
		else if(type == _Value){
			u_color = #aero
		}
		else if(type == _TypeVar){
			u_color = #00ff00
		}
		else if(type == _Comment){
			u_color = mix(#777,#ccc,.5+.5*sin(pos.x+pos.y-0.5time.sec))
		}
		else {
			u_color = #orange
		}
	}
	// needlessly animating default style
	style2:(){
        
        vec4 c//COMEABS
		vec4 m = marks[] // maal comments!
		vec2 pos = m.xy 
		vec2 center = pos - .5

		var group = m.z
		var type = floor(m.w / 65536.)
		var sub = mod(floor(m.w/256.), 256.)
		var part  = mod(m.w, 256.)
		
		if( sub == _Paren  ){
				
           u_color = weirdColor.iceBlue( pos , mesh[].xy )
   
        }else if( type ==  _Key ){

          // Setting outline inside weirdColor
          // is weird
          u_outline = true
          u_color = weirdColor.whiteDot( pos , mesh[].xy )

         
          
        }else if(sub == _Bracket){
              u_color = #aero 
              //#ccc//mix(#red, #yellow, abs on sin on group)
              u_boldness =  .3*sin on group
        }else if(sub == _Operator){

			u_color = weirdColor.redBludge( pos , mesh[].xy )
              
            //u_boldness = .5+0.7sin(8.*length(center)-4.*time.sec+group)
		}
		else if(type == _Id){
			u_color = weirdColor.redNight( pos , mesh[].xy )
			if(sub == _Color){
				u_color = #pink
			}


		}
		else if(type == _Value){
			u_color = weirdColor.whiteLight( pos , mesh[].xy )
        }
		else if(type == _TypeVar){
		  u_color = weirdColor.swampFire( pos , mesh[].xy ) 
		}else if(type == _Var){
		  u_color = weirdColor.galaxyPurple( pos , mesh[].xy ) 
		}else if(type == _Comment){
			u_color = weirdColor.starryNight( pos , mesh[].xy )
        }else if(type == _Class){
			u_color = #sky
		}
		else{
			u_color = weirdColor.sleepTime( pos , mesh[].xy ) 
		}
	}




              
}
